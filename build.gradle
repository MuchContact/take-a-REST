buildscript {
  repositories {
    jcenter()
  }
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'war'
apply plugin: 'idea'

repositories {
  mavenCentral()
}

configurations {
  integrationTestCompile {
    extendsFrom testCompile
  }
  integrationTestRuntime {
    extendsFrom integrationTestCompile, testRuntime
  }
}

def sets = [ "java", "groovy", "resources" ]
def sourceFolder = { set ->
  "src/integration-test/$set"
}

sourceSets {
  integrationTest { sourceSet ->
    sets.each {
      if (sourceSet.hasProperty(it)) {
        sourceSet."$it".srcDir file(sourceFolder(it))
      }
    }
  }
}

dependencies {
  providedCompile('org.jboss.resteasy:jaxrs-api:3.0.8.Final')
  providedCompile('javax.servlet:javax.servlet-api:3.1.0')
  providedCompile('javax.enterprise:cdi-api:1.1')
  providedCompile('org.jboss.resteasy:resteasy-jaxrs:3.0.8.Final')
  providedCompile('org.jboss.resteasy:resteasy-jackson2-provider:3.0.8.Final')
  compile('log4j:log4j:1.2.14')
  compile('org.projectlombok:lombok:1.14.2')
  compile('com.googlecode.siren4j:siren4j:1.0.14') {
    exclude group: 'com.sun.jersey'
  }
  compile('com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.4.0')
  compile('org.apache.commons:commons-lang3:3.3.2')
  compile('javax.validation:validation-api:1.1.0.Final')
  compile('org.hibernate:hibernate-validator:5.1.1.Final')
  compile('org.hibernate:hibernate-validator-cdi:5.1.1.Final')
  compile('com.theoryinpractise:halbuilder-standard:3.0.1')
  compile('com.google.guava:guava:17.0')

  integrationTestCompile sourceSets.main.output

  integrationTestCompile 'org.codehaus.groovy:groovy-all:2.3.3'
  integrationTestCompile('org.spockframework:spock-core:0.7-groovy-2.0') {
      exclude group: 'org.codehaus.groovy'
  }
  integrationTestCompile('org.jboss.resteasy:resteasy-client:3.0.8.Final')
  integrationTestCompile('org.skyscreamer:jsonassert:1.2.3')
}

task wrapper(type: Wrapper) {
  gradleVersion = '1.12'
}

group = 'org.letustakearest'
version = '1.0'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

idea {
  module {
    scopes.TEST.plus += configurations.integrationTestCompile
  }
}
